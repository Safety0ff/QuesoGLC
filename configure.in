#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(src/global.c)
AM_INIT_AUTOMAKE(QuesoGLC, 0.4)
AC_CONFIG_HEADER(include/qglc_config.h:include/qglc_config.hin)

MAJOR=0
MINOR=4
REVISION=0

CFLAGS=

AC_ARG_ENABLE(debug-mode, [  --enable-debug-mode[=ARG] build debug library [default=no] ])
AM_CONDITIONAL(DEBUGMODE, test "$enable_debug_mode" = yes)

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_LIBTOOL

dnl Test for location of X headers and libraries

AC_PATH_XTRA
if test -n "$x_includes"; then
	CPPFLAGS="-I$x_includes"
fi
if test -n "$x_libraries"; then
	LDFLAGS="-L$x_libraries"
fi
LIBS="$X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS"

# Checks for libraries.
AC_CHECK_LIB(pthread, pthread_create, [], [AC_MSG_ERROR([Unable to locate the required pthread library])])
AC_CHECK_LIB(GL, glBegin, [], [AC_MSG_ERROR([Unable to locate the required GL library])])
AC_CHECK_LIB(GLU, gluNewTess, [], [AC_MSG_ERROR([Unable to locate the required GLU library])])
CPPFLAGS="`freetype-config --cflags` $CPPFLAGS"
AC_CHECK_LIB(freetype, FT_Init_FreeType, [LIBS="`freetype-config --libs` $LIBS"], [AC_MSG_ERROR([Unable to locate the required FreeType library])])
AC_CHECK_LIB(fontconfig, FcInit, [], [AC_MSG_ERROR([Unable to locate the required fontconfig library])])
AC_CHECK_LIB(glut, glutInit, [AC_CHECK_HEADERS(GL/glut.h, [EXAMPLES="examples";TESTS_WITH_GLUT="testrender test7"])])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h unistd.h pthread.h GL/gl.h GL/glu.h ft2build.h fontconfig/fontconfig.h], [], [AC_MSG_ERROR([Unable to locate a required header])])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_FUNC_STAT
AC_CHECK_FUNCS([atexit memmove memset sqrt strdup])

AC_CONFIG_FILES([Makefile lib/Makefile tests/Makefile examples/Makefile])

AC_SUBST(MAJOR)
AC_SUBST(MINOR)
AC_SUBST(REVISION)
AC_SUBST(EXAMPLES)
AC_SUBST(TESTS_WITH_GLUT)
AC_OUTPUT
