#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)

# Init autoconf.
# --------------

AC_INIT([QuesoGLC], [0.6.0],
        [quesoglc-general@lists.sourceforge.net],
        [quesoglc])

AC_CONFIG_HEADER([include/qglc_config.h:include/qglc_config.hin])

AC_CONFIG_SRCDIR([src/global.c])
AC_CONFIG_AUX_DIR([build])


# Init automake.
# --------------

AM_INIT_AUTOMAKE([dist-bzip2])


# Checks for programs.
# --------------------

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_LIBTOOL


# Checks for multithreading support.
# ----------------------------------

ACX_PTHREAD

CC="$PTHREAD_CC"


# Checks for typedefs, structures, and compiler characteristics.
# --------------------------------------------------------------

AC_C_CONST

AC_TYPE_SIZE_T

AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)


# Checks for header files.
# ------------------------

AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h unistd.h], [],
                 [AC_MSG_ERROR([Unable to locate a required header])])


# Checks for C library features.
# ------------------------------

AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_FUNC_STAT
AC_CHECK_FUNCS([atexit memmove memset strdup])

AC_CHECK_LIB([m], [sqrt])


# Checks for font libraries.
# --------------------------

AX_CHECK_FREETYPE2

if (test "x$no_ft2" = "xyes"); then
  AC_MSG_ERROR([Unable to locate the required FreeType library])
fi

AX_CHECK_FONTCONFIG

if (test "x$no_fc" = "xyes"); then
  AC_MSG_ERROR([Unable to locate the required Fontconfig library])
fi


# Checks for OpenGL and related libraries.
# ----------------------------------------

AX_CHECK_GL

if (test "x$no_gl" = "xyes"); then
  AC_MSG_ERROR([OpenGL not found])
fi

AX_CHECK_GLU

if (test "x$no_glu" = "xyes"); then
  AC_MSG_ERROR([GLU library not found])
fi

AX_CHECK_GLUT

if (test "x$no_glut" = "xyes"); then
  echo "*** The GLUT library could not be found : example programs and some"
  echo "*** tests will not be built"
else
  EXAMPLES="examples"
  TESTS_WITH_GLUT="test7 testrender"
fi


# Output.
# -------

AC_CONFIG_FILES(Makefile
                build/Makefile
                include/Makefile
                tests/Makefile
                examples/Makefile)

AC_SUBST(EXAMPLES)
AC_SUBST(TESTS_WITH_GLUT)
AC_OUTPUT
